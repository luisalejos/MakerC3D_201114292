package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER;
terminal TokenC3D IGUAL, DIFERENTE, MAYOR, MENOR, MAYOR_IGUAL, MENOR_IGUAL, AND, OR, NOT;
nonterminal NodoC3D inicial, expresion;

precedence left OR;
precedence left AND;
precedence left IGUAL, DIFERENTE;
precedence left MAYOR, MENOR, MAYOR_IGUAL, MENOR_IGUAL;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT;
//precedence right NOT; 


start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} expresion:exp 
                    {: 
                        RESULT = exp;
                        ControlC3D.agregarC3D(exp.getCad());
                        ControlC3D.agregarC3D(exp.getVerdadera()+":\n");
                        ControlC3D.agregarC3D(exp.getFalsa()+":\n");
                        
                        ControlC3D.agregarC3D("/* Ãšltimo valor: " + exp.getCad() + "*/");
                    :};

expresion   ::= expresion:izq OR expresion:der
                    {: 
                        String verdadera = izq.getVerdadera()+", "+ der.getVerdadera();
                        String falsa = der.getFalsa();
                        
                        String c3d = izq.getCad() + " " + izq.getFalsa() +":\n";
                        c3d+= der.getCad() + "\n";
                        
                        RESULT = new NodoC3D(c3d, verdadera, falsa);
                       
                    :}
            |   expresion:izq AND expresion:der
                    {: 
                        String verdadera = der.getVerdadera();
                        String falsa =izq.getFalsa()+", "+ der.getFalsa();
                        
                        String c3d = izq.getCad() + " " + izq.getVerdadera() +":\n";
                        c3d+= der.getCad() + "\n";
                        
                        RESULT = new NodoC3D(c3d, verdadera, falsa); 
                    :}
            |   expresion:izq IGUAL expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " == " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa); 
                    :}
            |   expresion:izq DIFERENTE expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " != " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa);  
                    :}
            |   expresion:izq MAYOR expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " > " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa); 
                    :}
            |   expresion:izq MENOR expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " < " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa); 
                    :}
            |   expresion:izq MAYOR_IGUAL expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " >= " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa);  
                    :}
            |   expresion:izq MENOR_IGUAL expresion:der
                    {: 
                        String verdadera = ControlC3D.generaEtiqueta();
                        String falsa = ControlC3D.generaEtiqueta();
                        
                        String c3d = "if " + izq.getCad() + " <= " + der.getCad() + " goto "+ verdadera +"\n";
                        c3d += "goto "+ falsa +"\n";
                        //ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(c3d,verdadera, falsa); 
                    :}
            |   expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   NOT expresion:exp 
                    {: 
                        RESULT = new NodoC3D(exp.getCad(), exp.getFalsa(), exp.getVerdadera()); 
                    :}
            |   PAR_IZQ expresion:exp PAR_DER   
                    {: RESULT = exp; :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;